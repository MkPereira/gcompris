dnl Process this file with autoconf to produce a configure script.

AC_INIT(src/gcompris/gcompris.c)
AM_INIT_AUTOMAKE(gcompris, 6.0)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

# Making releases:
#   LIBGCOMPRIS_MICRO_VERSION += 1;
#   LIBGCOMPRIS_INTERFACE_AGE += 1;
# if any functions have been added, set LIBGCOMPRIS_INTERFACE_AGE to 0.
# if backwards compatibility has been broken,
# set LIBGCOMPRIS_BINARY_AGE and LIBGCOMPRIS_INTERFACE_AGE to 0.
#
LIBGCOMPRIS_MAJOR_VERSION=1
LIBGCOMPRIS_MINOR_VERSION=0
LIBGCOMPRIS_MICRO_VERSION=0
LIBGCOMPRIS_INTERFACE_AGE=0
# If you need a modifier for the version number. 
# Normally empty, but can be used to make "fixup" releases.
LIBGCOMPRIS_EXTRAVERSION=

dnl libtool versioning from libgnome

LIBGCOMPRIS_CURRENT=`expr 100 '*' $LIBGCOMPRIS_MINOR_VERSION + $LIBGCOMPRIS_MICRO_VERSION - $LIBGCOMPRIS_INTERFACE_AGE`
LIBGCOMPRIS_BINARY_AGE=`expr 100 '*' $LIBGCOMPRIS_MINOR_VERSION + $LIBGCOMPRIS_MICRO_VERSION`
LIBGCOMPRIS_REVISION=$LIBGCOMPRIS_INTERFACE_AGE
LIBGCOMPRIS_AGE=`expr $LIBGCOMPRIS_BINARY_AGE - $LIBGCOMPRIS_INTERFACE_AGE`
LIBGCOMPRIS_VERSION=$LIBGCOMPRIS_MAJOR_VERSION.$LIBGCOMPRIS_MINOR_VERSION.$LIBGCOMPRIS_MICRO_VERSION$LIBGCOMPRIS_EXTRAVERSION

AC_SUBST(LIBGCOMPRIS_CURRENT)
AC_SUBST(LIBGCOMPRIS_REVISION)
AC_SUBST(LIBGCOMPRIS_AGE)

AC_ISC_POSIX

GTK_REQUIRED=2.0.3
GDK_PIXBUF_REQUIRED=2.0.6
LIBGNOMECANVAS_REQUIRED=2.0.2

PKG_CHECK_MODULES(GCOMPRIS, gtk+-2.0 >= $GTK_REQUIRED gdk-pixbuf-2.0 >= $GDK_PIXBUF_REQUIRED libgnomecanvas-2.0 >= $LIBGNOMECANVAS_REQUIRED gthread-2.0)
AC_CHECK_LIB(SDL_mixer, Mix_OpenAudio,, AC_MSG_ERROR([*** SDL_mixer not found. Visit http://www.libsdl.org and get it]))

AC_SUBST(GCOMPRIS_CFLAGS)
AC_SUBST(GCOMPRIS_LIBS)

dnl gcompris-edit still require gnome
GCOMPRIS_EDIT_CFLAGS=
GCOMPRIS_EDIT_LIBS=
dnl By default, build gcompris-edit
GCOMPRIS_EDIT=yes
build_gcompris_edit=yes
AC_ARG_WITH(editor,
  AC_HELP_STRING(
    [--with-editor],
    [Tell configure to compile gcompris-edit (it requires gnome)]),
  [GCOMPRIS_EDIT=$withval] )

if test x$GCOMPRIS_EDIT = xno; then
  build_gcompris_edit=no
fi

dnl Finally output the required python variables and automake defines.
AM_CONDITIONAL(GCOMPRIS_EDIT, test x$build_gcompris_edit = xyes)

if test x$build_gcompris_edit = xno; then
  AC_MSG_NOTICE([gcompris-edit build is disabled.])
else
  dnl gcompris-edit is requested, so check for gnome
  LIBGNOME_REQUIRED=1.96.0
  LIBGNOMEUI_REQUIRED=1.96.0

  PKG_CHECK_MODULES(GCOMPRIS_EDIT, libgnome-2.0 >= $LIBGNOME_REQUIRED libgnomeui-2.0 >= $LIBGNOMEUI_REQUIRED)

  AC_SUBST(GCOMPRIS_EDIT_CFLAGS)
  AC_SUBST(GCOMPRIS_EDIT_LIBS)

fi

AC_HEADER_DIRENT

AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC

AC_PROG_INTLTOOL

AM_DISABLE_STATIC
AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL

dnl Check for SDL
SDL_VERSION=1.2.3
AM_PATH_SDL($SDL_VERSION,
            :,
	    AC_MSG_ERROR([*** SDL version $SDL_VERSION not found!])
)

dnl GNOME2 GNOME_X_CHECKS
AM_PATH_GLIB_2_0

dnl libxml-2 Checks
PKG_CHECK_MODULES(XML, libxml-2.0)
AC_SUBST(XML_CFLAGS)
AC_SUBST(XML_LIBS)

dnl glib-genmarshal
AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)

dnl popt
AC_CHECK_LIB(popt, poptDupArgv,,
AC_MSG_ERROR([You must have popt 1.5 or newer to compile gcompris.]))

AM_ICONV

dnl Add the languages which your application supports here.
ALL_LINGUAS="am ar az ca cs de el en_CA en_GB es fi fr he hi hr hu it lt mk ml ms nl no pl pt pt_BR ro ru sk sl sr sr@Latn sv tr"

dnl GCompris needs to know which locale are supported
AC_SUBST(ALL_LINGUAS)
AC_DEFINE_UNQUOTED(ALL_LINGUAS, "${ALL_LINGUAS}", [Supported languages])

GETTEXT_PACKAGE=gcompris
AC_SUBST(GETTEXT_PACKAGE)
AM_GLIB_GNU_GETTEXT
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE", [Gettext package name])

dnl Define myprefix depending on wether our user gives us one or not
if test "x${prefix}" = "xNONE"; then
  myprefix=${ac_default_prefix}
else
  myprefix=${prefix}
fi

dnl Some debug option
AC_ARG_ENABLE(debug,
	      AC_HELP_STRING(
	      [--enable-debug],
	      [Turn on debugging messages]),
	      [ AC_DEFINE_UNQUOTED(DEBUG, 1, "Enable debug messages.") CFLAGS="$CFLAGS -Wall -Werror"] )

dnl WIN32 Specifics
AC_MSG_CHECKING([for native Win32])
case "$host" in
  *-*-mingw*)
    native_win32=yes
    ;;
  *)
    native_win32=no
    ;;
esac
AC_MSG_RESULT([$native_win32])
AM_CONDITIONAL(OS_WIN32, test "$native_win32" = yes)

AC_MSG_CHECKING([for Win32 platform in general])
case "$host" in
  *-*-mingw*|*-*-cygwin*)
    platform_win32=yes
    ;;
  *)
    platform_win32=no
    ;;
esac
AC_MSG_RESULT($platform_win32)
AM_CONDITIONAL(PLATFORM_WIN32, test "$platform_win32" = yes)

# Ensure MSVC-compatible struct packing convention is used when
# compiling for Win32 with gcc. GTK+ uses this convention, so we must, too.
# What flag to depends on gcc version: gcc3 uses "-mms-bitfields", while
# gcc2 uses "-fnative-struct".
if test x"$native_win32" = xyes; then
  if test x"$GCC" = xyes; then
    msnative_struct=''
    AC_MSG_CHECKING([how to get MSVC-compatible struct packing])
    if test -z "$ac_cv_prog_CC"; then
      our_gcc="$CC"
    else
      our_gcc="$ac_cv_prog_CC"
    fi
    case `$our_gcc --version | sed -e 's,\..*,.,' -e q` in
      2.)
	if $our_gcc -v --help 2>/dev/null | grep fnative-struct >/dev/null; then
	  msnative_struct='-fnative-struct'
	fi
	;;
      *)
	if $our_gcc -v --help 2>/dev/null | grep ms-bitfields >/dev/null; then
	  msnative_struct='-mms-bitfields'
	fi
	;;
    esac
    if test x"$msnative_struct" = x ; then
      AC_MSG_RESULT([no way])
      AC_MSG_WARN([produced libraries will be incompatible with prebuilt GTK+ DLLs])
    else
      CFLAGS="$CFLAGS $msnative_struct"
      AC_MSG_RESULT([${msnative_struct}])
    fi
  fi
fi

dnl Set PACKAGE_LOCALE_DIR in config.h.
AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${myprefix}/${DATADIRNAME}/locale", [Gcompris locale directory])

dnl Set PACKAGE_SOUNDS_DIR
PACKAGE_SOUNDS_DIR="sounds"
AC_SUBST(PACKAGE_SOUNDS_DIR)
AC_DEFINE_UNQUOTED(PACKAGE_SOUNDS_DIR, "${myprefix}/${DATADIRNAME}/${PACKAGE}/sounds", [Gcompris sounds directory])

dnl Set PACKAGE_DATA_DIR
PACKAGE_DATA_DIR="boards"
AC_SUBST(PACKAGE_DATA_DIR)
AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${myprefix}/${DATADIRNAME}/${PACKAGE}/boards", [Gcompris data directory])

dnl Set PACKAGE_HELP_DIR
AC_DEFINE_UNQUOTED(PACKAGE_HELP_DIR, "${myprefix}/${DATADIRNAME}/gnome/help/${PACKAGE}", [Gcompris help directory])
PACKAGE_HELP_DIR="${myprefix}/${DATADIRNAME}/gnome/help/${PACKAGE}"
AC_SUBST(PACKAGE_HELP_DIR)

dnl Plugin Directory
AC_DEFINE_UNQUOTED(PLUGIN_DIR, "${myprefix}/lib/gcompris", [Gcompris plugins directory])

AC_DEFINE_UNQUOTED(GNOME_ICONDIR, "${myprefix}/${DATADIRNAME}/pixmaps", [Gnome icons directory])

plugindir=$libdir/gcompris
AC_SUBST(plugindir)

dnl Test for gnuchess
AC_PATH_PROG(GNUCHESS, gnuchess,no,[/usr/bin:/usr/games:/usr/local/bin])

if test x$GNUCHESS = xno; then
  AC_MSG_ERROR(Couldn't find gnuchess, please install the gnuchess package version 5 or above)
fi
AC_DEFINE_UNQUOTED(GNUCHESS, "$GNUCHESS", Defines where GNU Chess resides on the system)

AC_PATH_PROG(TEXINFO, makeinfo,no)
if test x$TEXINFO = xno; then
  AC_MSG_ERROR(Couldn't find texinfo, please install the texinfo package)
fi

AC_PATH_PROG(TETEX, texi2html,no)
if test x$TETEX = xno; then
  AC_MSG_ERROR(Couldn't find texi2html usualy in the tetex package, please install it)
fi

dnl Tests for the python plugin
PYTHON_CFLAGS=
PYTHON_LIBS=
dnl By default, dont build python until we have something usefull to show
PYTHON_PATH=yes
build_python_plugin=yes
AC_ARG_WITH(python,
  AC_HELP_STRING(
    [--with-python=path],
    [Give the python interpreter (ex: /opt/python/bin/python2.2)]),
  [PYTHON_PATH=$withval] )

if test x$PYTHON_PATH = xno; then
  build_python_plugin=no
else
  dnl Find a python interpretter
  python_old_path=${PATH}
  if test x$PYTHON_PATH = xyes; then
    AC_PATH_PROG(python_exec, python, no)
    if test x$python_exec = xno; then
      build_python_plugin=no
      AC_MSG_WARN([Couldn't find python interpreter, python plugin disabled.])
    fi
  else
    if test -x "${PYTHON_PATH}"; then
      python_exec=${PYTHON_PATH}
    else
      build_python_plugin=no
      AC_MSG_WARN([Couldn't find python interpreter, python plugin disabled.])
    fi
  fi

  if test x$build_python_plugin = xyes; then
    python_version=`${python_exec} -c "import sys; print sys.version[[:3]]"`
    python_prefix=`${python_exec} -c "import sys; print sys.prefix"`
    python_exec_prefix=`${python_exec} -c "import sys; print sys.exec_prefix"`

    dnl Search for python's includes
    python_old_cppflags=${CPPFLAGS}
    CPPFLAGS="${CPPFLAGS} -I${python_prefix}/include/python${python_version}"
    AC_CHECK_HEADER(Python.h, 
                    PYTHON_CFLAGS="-I${python_prefix}/include/python${python_version}",
                    [ AC_MSG_WARN([Couldn't find python includes, python plugin disabled.])
                      build_python_plugin=no])
    CPPFLAGS=$python_old_cppflags
  fi
  if test x$build_python_plugin = xyes; then
    dnl Search for python's libs
    dnl Take the needed libraries into the lib/python/config/Makefile... (see dia config)
    python_makefile="${python_exec_prefix}/lib/python${python_version}/config/Makefile"
    if test -f "${python_makefile}"; then
    dnl extra required libs
      python_localmodlibs=`sed -n -e 's/^LOCALMODLIBS=\(.*\)/\1/p' $python_makefile`
      python_basemodlibs=`sed -n -e 's/^BASEMODLIBS=\(.*\)/\1/p' $python_makefile`
      python_other_libs=`sed -n -e 's/^LIBS=\(.*\)/\1/p' $python_makefile`
      python_libs_m=`sed -n -e 's/^LIBM=\(.*\)/\1/p' $python_makefile`
      python_libs_cflags=`sed -n -e 's/^LINKFORSHARED=\(.*\)/\1/p' $python_makefile`
      python_libs="${python_localmodlibs} ${python_basemodlibs} ${python_other_libs} ${python_libs_m}"

      python_old_ldflags=${LDFLAGS}
      LDFLAGS="${LDFLAGS} ${python_libs_cflags} -L${python_exec_prefix}/lib/python${python_version}/config ${python_libs}"
      AC_CHECK_LIB(python${python_version}, 
                   Py_Initialize, 
                   PYTHON_LIBS="${python_libs_cflags} -L${python_exec_prefix}/lib/python${python_version}/config -lpython${python_version} ${python_libs}",
                   [ AC_MSG_WARN([Couldn't find python library, python plugin disabled.])
                     build_python_plugin=no ])  
      LDFLAGS=${python_old_ldflags}
    else
      AC_MSG_WARN([Couldn't find python library, python plugin disabled.])
      build_python_plugin=no
    fi
  fi
fi

dnl Finally output the required python variables and automake defines.
AM_CONDITIONAL(PYTHON_PLUGIN, test x$build_python_plugin = xyes)

AC_SUBST(PYTHON_CFLAGS)
AC_SUBST(PYTHON_LIBS)

REQUIRE_PYHTON=""
if test x$build_python_plugin = xno; then
  AC_MSG_NOTICE([Python plugin is disabled.])
else
  dnl Python interpreter is available so check for pygtk and gnome-python
  PKG_CHECK_MODULES(PYGTK, pygtk-2.0 gnome-python-2.0)	
  AC_SUBST(PYGTK_CFLAGS)
  AC_SUBST(PYGTK_LIBS)
  REQUIRE_PYTHON="python gnome-python gnome-python-canvas pygtk2.0"
fi

AC_SUBST(REQUIRE_PYTHON)

dnl For libassetml
ASSETML_DIR=assetml
AC_SUBST(ASSETML_DIR)
AC_DEFINE_UNQUOTED(ASSETML_DIR, "${myprefix}/${DATADIRNAME}/${ASSETML_DIR}", [Global assetml directory])

dnl assetml version management
# DE
VERSION_VOICES_ALPHABET_DE=1.0
AC_SUBST(VERSION_VOICES_ALPHABET_DE)

VERSION_VOICES_COLORS_DE=1.0
AC_SUBST(VERSION_VOICES_COLORS_DE)

VERSION_VOICES_GEOGRAPHY_DE=0.0
AC_SUBST(VERSION_VOICES_GEOGRAPHY_DE)

VERSION_VOICES_MISC_DE=1.0
AC_SUBST(VERSION_VOICES_MISC_DE)

# EN
VERSION_VOICES_ALPHABET_EN=1.0
AC_SUBST(VERSION_VOICES_ALPHABET_EN)

VERSION_VOICES_COLORS_EN=1.1
AC_SUBST(VERSION_VOICES_COLORS_EN)

VERSION_VOICES_GEOGRAPHY_EN=1.1
AC_SUBST(VERSION_VOICES_GEOGRAPHY_EN)

VERSION_VOICES_MISC_EN=1.0
AC_SUBST(VERSION_VOICES_MISC_EN)

# ES
VERSION_VOICES_ALPHABET_ES=1.0
AC_SUBST(VERSION_VOICES_ALPHABET_ES)

VERSION_VOICES_COLORS_ES=1.0
AC_SUBST(VERSION_VOICES_COLORS_ES)

VERSION_VOICES_GEOGRAPHY_ES=1.0
AC_SUBST(VERSION_VOICES_GEOGRAPHY_ES)

VERSION_VOICES_MISC_ES=1.0
AC_SUBST(VERSION_VOICES_MISC_ES)

# FR
VERSION_VOICES_ALPHABET_FR=1.1
AC_SUBST(VERSION_VOICES_ALPHABET_FR)

VERSION_VOICES_COLORS_FR=1.0
AC_SUBST(VERSION_VOICES_COLORS_FR)

VERSION_VOICES_GEOGRAPHY_FR=1.1
AC_SUBST(VERSION_VOICES_GEOGRAPHY_FR)

VERSION_VOICES_MISC_FR=1.1
AC_SUBST(VERSION_VOICES_MISC_FR)

VERSION_VOICES_FRANCE_REGIONS_FR=1.0
AC_SUBST(VERSION_VOICES_FRANCE_REGIONS_FR)

# IT
VERSION_VOICES_ALPHABET_IT=1.0
AC_SUBST(VERSION_VOICES_ALPHABET_IT)

VERSION_VOICES_COLORS_IT=1.0
AC_SUBST(VERSION_VOICES_COLORS_IT)

VERSION_VOICES_GEOGRAPHY_IT=1.0
AC_SUBST(VERSION_VOICES_GEOGRAPHY_IT)

VERSION_VOICES_MISC_IT=1.0
AC_SUBST(VERSION_VOICES_MISC_IT)

# PT
VERSION_VOICES_ALPHABET_PT=1.0
AC_SUBST(VERSION_VOICES_ALPHABET_PT)

VERSION_VOICES_COLORS_PT=1.0
AC_SUBST(VERSION_VOICES_COLORS_PT)

VERSION_VOICES_GEOGRAPHY_PT=1.1
AC_SUBST(VERSION_VOICES_GEOGRAPHY_PT)

VERSION_VOICES_MISC_PT=1.1
AC_SUBST(VERSION_VOICES_MISC_PT)

VERSION_FLAGS=1.1
AC_SUBST(VERSION_FLAGS)


dnl Autoconf output
AC_OUTPUT([ gcompris.spec
Makefile
gcompris.desktop
gcompris-edit.desktop
src/Makefile
src/gcompris/Makefile
src/gcompris/libgcompris-1.0.pc
src/gcompris/libgcompris-1.0-uninstalled.pc
src/boards/Makefile
src/boards/python/Makefile
src/boards/python/gcompris/Makefile
src/boards/python/gcompris/bonus/Makefile
src/boards/python/gcompris/score/Makefile
src/boards/python/gcompris/skin/Makefile
src/boards/python/gcompris/sound/Makefile
src/boards/python/gcompris/timer/Makefile
src/boards/python/gcompris/utils/Makefile
po/Makefile.in
docs/Makefile
docs/C/Makefile
docs/fr/Makefile
boards/Makefile
boards/advanced_colors/Makefile
boards/babymatch/Makefile
boards/babyshapes/Makefile
boards/chronos/Makefile
boards/doubleentry/Makefile
boards/flags/Makefile
boards/geography/Makefile
boards/imageid/Makefile
boards/imagename/Makefile
boards/missing_letter/Makefile
boards/paintings/Makefile
boards/read_colors/Makefile
boards/scales/Makefile
boards/sounds/Makefile
boards/sounds/de/Makefile
boards/sounds/de/alphabet/Makefile
boards/sounds/de/colors/Makefile
boards/sounds/de/geography/Makefile
boards/sounds/de/misc/Makefile
boards/sounds/en/Makefile
boards/sounds/en/alphabet/Makefile
boards/sounds/en/colors/Makefile
boards/sounds/en/geography/Makefile
boards/sounds/en/misc/Makefile
boards/sounds/es/Makefile
boards/sounds/es/alphabet/Makefile
boards/sounds/es/colors/Makefile
boards/sounds/es/geography/Makefile
boards/sounds/es/misc/Makefile
boards/sounds/fr/Makefile
boards/sounds/fr/alphabet/Makefile
boards/sounds/fr/colors/Makefile
boards/sounds/fr/france_region/Makefile
boards/sounds/fr/geography/Makefile
boards/sounds/fr/misc/Makefile
boards/sounds/it/Makefile
boards/sounds/it/alphabet/Makefile
boards/sounds/it/colors/Makefile
boards/sounds/it/geography/Makefile
boards/sounds/it/misc/Makefile
boards/sounds/pt/Makefile
boards/sounds/pt/alphabet/Makefile
boards/sounds/pt/colors/Makefile
boards/sounds/pt/geography/Makefile
boards/sounds/pt/misc/Makefile
])

echo ""
echo "***************************************************"
echo ""
echo configure complete,
echo with options:
echo "python plugins = $build_python_plugin"
echo "gcompris edit  = $build_gcompris_edit"

if test -x "${SDL_LIBS}"; then
echo "SDL LIBS       = NOT DETECTED (install sdl-devel)"
else
echo "SDL LIBS       = FOUND"
fi

echo gcompris will be installed in ${prefix}
echo to compile and install in in another directory
echo type sh configure --prefix=/usr
echo
echo type \'make\' to compile gcompris
echo type \'make install\' to install it
