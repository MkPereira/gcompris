dnl Process this file with autoconf to produce a configure script.

AC_INIT(src/gcompris/gcompris.c)
AC_CANONICAL_TARGET
AM_INIT_AUTOMAKE(gcompris, 6.5.3)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

# Making releases:
#   LIBGCOMPRIS_MICRO_VERSION += 1;
#   LIBGCOMPRIS_INTERFACE_AGE += 1;
# if any functions have been added, set LIBGCOMPRIS_INTERFACE_AGE to 0.
# if backwards compatibility has been broken,
# set LIBGCOMPRIS_BINARY_AGE and LIBGCOMPRIS_INTERFACE_AGE to 0.
#
LIBGCOMPRIS_MAJOR_VERSION=1
LIBGCOMPRIS_MINOR_VERSION=0
LIBGCOMPRIS_MICRO_VERSION=0
LIBGCOMPRIS_INTERFACE_AGE=0
# If you need a modifier for the version number. 
# Normally empty, but can be used to make "fixup" releases.
LIBGCOMPRIS_EXTRAVERSION=

dnl libtool versioning from libgnome

LIBGCOMPRIS_CURRENT=`expr 100 '*' $LIBGCOMPRIS_MINOR_VERSION + $LIBGCOMPRIS_MICRO_VERSION - $LIBGCOMPRIS_INTERFACE_AGE`
LIBGCOMPRIS_BINARY_AGE=`expr 100 '*' $LIBGCOMPRIS_MINOR_VERSION + $LIBGCOMPRIS_MICRO_VERSION`
LIBGCOMPRIS_REVISION=$LIBGCOMPRIS_INTERFACE_AGE
LIBGCOMPRIS_AGE=`expr $LIBGCOMPRIS_BINARY_AGE - $LIBGCOMPRIS_INTERFACE_AGE`
LIBGCOMPRIS_VERSION=$LIBGCOMPRIS_MAJOR_VERSION.$LIBGCOMPRIS_MINOR_VERSION.$LIBGCOMPRIS_MICRO_VERSION$LIBGCOMPRIS_EXTRAVERSION

AC_SUBST(LIBGCOMPRIS_CURRENT)
AC_SUBST(LIBGCOMPRIS_REVISION)
AC_SUBST(LIBGCOMPRIS_AGE)

AC_ISC_POSIX

GTK_REQUIRED=2.4.0
GDK_PIXBUF_REQUIRED=2.4.0
LIBGNOMECANVAS_REQUIRED=2.3.6

PKG_CHECK_MODULES(GCOMPRIS, gtk+-2.0 >= $GTK_REQUIRED gdk-pixbuf-2.0 >= $GDK_PIXBUF_REQUIRED libgnomecanvas-2.0 >= $LIBGNOMECANVAS_REQUIRED gthread-2.0)
AC_CHECK_LIB(SDL_mixer, Mix_OpenAudio,, AC_MSG_ERROR([*** SDL_mixer not found. Visit http://www.libsdl.org and get it]))

AC_SUBST(GCOMPRIS_CFLAGS)
AC_SUBST(GCOMPRIS_LIBS)

dnl PROFILING support
AC_MSG_CHECKING([wether we build with profils])
AC_ARG_ENABLE(profils,
          AC_HELP_STRING(
          [--enable-profils],
          [Turn on profils (needs sqlite)]),
          with_profils="$enableval", with_profils="no")
AC_MSG_RESULT($with_profils)

dnl gcompris-edit is incompatible with profils.
if test x$with_profils = xno; then
dnl gcompris-edit still require gnome
GCOMPRIS_EDIT_CFLAGS=
GCOMPRIS_EDIT_LIBS=
dnl By default, build gcompris-edit
GCOMPRIS_EDIT=yes
build_gcompris_edit=yes
AC_ARG_WITH(editor,
  AC_HELP_STRING(
    [--with-editor],
    [Tell configure to compile gcompris-edit (it requires gnome)]),
  [GCOMPRIS_EDIT=$withval] )
else
  GCOMPRIS_EDIT=no
fi

if test x$GCOMPRIS_EDIT = xno; then
  build_gcompris_edit=no
fi

dnl Finally output the required python variables and automake defines.
AM_CONDITIONAL(GCOMPRIS_EDIT, test x$build_gcompris_edit = xyes)

if test x$build_gcompris_edit = xno; then
  AC_MSG_NOTICE([gcompris-edit build is disabled.])
else
  dnl gcompris-edit is requested, so check for gnome
  LIBGNOME_REQUIRED=1.96.0
  LIBGNOMEUI_REQUIRED=1.96.0

  PKG_CHECK_MODULES(GCOMPRIS_EDIT, libgnome-2.0 >= $LIBGNOME_REQUIRED libgnomeui-2.0 >= $LIBGNOMEUI_REQUIRED)

  AC_SUBST(GCOMPRIS_EDIT_CFLAGS)
  AC_SUBST(GCOMPRIS_EDIT_LIBS)

fi

AC_HEADER_DIRENT

AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC

AC_PROG_INTLTOOL

AM_DISABLE_STATIC
AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL

dnl Check for SDL
SDL_VERSION=1.2.3
AM_PATH_SDL($SDL_VERSION,
            :,
	    AC_MSG_ERROR([*** SDL version $SDL_VERSION not found!])
)

dnl GNOME2 GNOME_X_CHECKS
AM_PATH_GLIB_2_0

dnl libxml-2 Checks
PKG_CHECK_MODULES(XML, libxml-2.0)
AC_SUBST(XML_CFLAGS)
AC_SUBST(XML_LIBS)

dnl glib-genmarshal
AC_PATH_PROG(GLIB_GENMARSHAL, glib-genmarshal)

dnl popt
AC_CHECK_LIB(popt, poptDupArgv,,
AC_MSG_ERROR([You must have popt 1.5 or newer to compile gcompris.]))

AM_ICONV

dnl XRANDR Allow us to set the screen resolution dynamically
AC_ARG_ENABLE(xrandr,
          AC_HELP_STRING(
          [--disable-xrandr],
          [Turn off xrandr]),
          USE_XRANDR="$enableval", USE_XRANDR="yes")
found_randr=no
if test "x$USE_XRANDR" == "xyes" ; then
  RANDR_LIBS=
  AC_CHECK_LIB(Xrandr, XRRSetScreenConfigAndRate,
             [AC_CHECK_HEADER(X11/extensions/Xrandr.h,
                              RANDR_LIBS="-L/usr/X11R6/lib/ -lXrandr" found_randr=yes,,
                                [#include <X11/Xlib.h>])],
               , -L/usr/X11R6/lib/ -lXrandr $ALL_X_LIBS )

  if test "x$found_randr" = "xno"; then
     AC_MSG_NOTICE(["Suitable Xrandr extension library not found - you need at least X 4.3. Will not use xrandr for fullscreen."])
     AC_DEFINE_UNQUOTED(XRANDR, 0, [XRANDR NOT Available])
  else
     AC_DEFINE_UNQUOTED(XRANDR, 1, [XRANDR Available])
  fi
fi

LDFLAGS="${LDFLAGS} ${RANDR_LIBS}"

dnl Add the languages which your application supports here.
ALL_LINGUAS="am ar az bg ca cs da de el en_CA en_GB es et fi fr ga gu he hi hr hu it lt mk ml ms nb nl nn pa pl pt pt_BR ro ru rw sk sl sq sr sr@Latn sv tr wa zh_CN"

dnl GCompris needs to know which locale are supported
AC_SUBST(ALL_LINGUAS)
AC_DEFINE_UNQUOTED(ALL_LINGUAS, "${ALL_LINGUAS}", [Supported languages])

GETTEXT_PACKAGE=gcompris
AC_SUBST(GETTEXT_PACKAGE)
AM_GLIB_GNU_GETTEXT
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE,"$GETTEXT_PACKAGE", [Gettext package name])

dnl Define myprefix depending on wether our user gives us one or not
if test "x${prefix}" = "xNONE"; then
  myprefix=${ac_default_prefix}
else
  myprefix=${prefix}
fi

dnl Some debug option
AC_ARG_ENABLE(debug,
          AC_HELP_STRING(
          [--enable-debug],
          [Turn on debugging messages]),
          USE_DEBUG="$enableval", USE_DEBUG="no")
if test "x$USE_DEBUG" == "xyes" ; then
    AC_DEFINE_UNQUOTED(DEBUG, 1, "Enable debug messages.")
    CFLAGS="$CFLAGS -Wall -g"
fi

dnl WIN32 Specifics
AC_MSG_CHECKING([for native Win32])
case "$host" in
  *-*-mingw*)
    native_win32=yes
    ;;
  *)
    native_win32=no
    ;;
esac
AC_MSG_RESULT([$native_win32])
AM_CONDITIONAL(OS_WIN32, test "$native_win32" = yes)

AC_MSG_CHECKING([for Win32 platform in general])
case "$host" in
  *-*-mingw*|*-*-cygwin*)
    platform_win32=yes
    ;;
  *)
    platform_win32=no
    ;;
esac
AC_MSG_RESULT($platform_win32)
AM_CONDITIONAL(PLATFORM_WIN32, test "$platform_win32" = yes)

# Ensure MSVC-compatible struct packing convention is used when
# compiling for Win32 with gcc. GTK+ uses this convention, so we must, too.
# What flag to depends on gcc version: gcc3 uses "-mms-bitfields", while
# gcc2 uses "-fnative-struct".
if test x"$native_win32" = xyes; then
  if test x"$GCC" = xyes; then
    msnative_struct=''
    AC_MSG_CHECKING([how to get MSVC-compatible struct packing])
    if test -z "$ac_cv_prog_CC"; then
      our_gcc="$CC"
    else
      our_gcc="$ac_cv_prog_CC"
    fi
    case `$our_gcc --version | sed -e 's,\..*,.,' -e q` in
      2.)
	if $our_gcc -v --help 2>/dev/null | grep fnative-struct >/dev/null; then
	  msnative_struct='-fnative-struct'
	fi
	;;
      *)
	if $our_gcc -v --help 2>/dev/null | grep ms-bitfields >/dev/null; then
	  msnative_struct='-mms-bitfields'
	fi
	;;
    esac
    if test x"$msnative_struct" = x ; then
      AC_MSG_RESULT([no way])
      AC_MSG_WARN([produced libraries will be incompatible with prebuilt GTK+ DLLs])
    else
      CFLAGS="$CFLAGS $msnative_struct"
      AC_MSG_RESULT([${msnative_struct}])
    fi
  fi
fi

dnl Set PACKAGE_LOCALE_DIR in config.h.
AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${myprefix}/${DATADIRNAME}/locale", [Gcompris locale directory])

dnl Set PACKAGE_SOUNDS_DIR
PACKAGE_SOUNDS_DIR="sounds"
AC_SUBST(PACKAGE_SOUNDS_DIR)
AC_DEFINE_UNQUOTED(PACKAGE_SOUNDS_DIR, "${myprefix}/${DATADIRNAME}/${PACKAGE}/sounds", [Gcompris sounds directory])

dnl Set PACKAGE_DATA_DIR
PACKAGE_DATA_DIR="boards"
AC_SUBST(PACKAGE_DATA_DIR)
AC_DEFINE_UNQUOTED(PACKAGE_DATA_DIR, "${myprefix}/${DATADIRNAME}/${PACKAGE}/boards", [Gcompris data directory])

dnl Set PACKAGE_HELP_DIR
AC_DEFINE_UNQUOTED(PACKAGE_HELP_DIR, "${myprefix}/${DATADIRNAME}/gnome/help/${PACKAGE}", [Gcompris help directory])
PACKAGE_HELP_DIR="${myprefix}/${DATADIRNAME}/gnome/help/${PACKAGE}"
AC_SUBST(PACKAGE_HELP_DIR)

dnl Plugin Directory
AC_DEFINE_UNQUOTED(PLUGIN_DIR, "${myprefix}/lib/gcompris", [Gcompris plugins directory])
plugindir=$libdir/gcompris
AC_SUBST(plugindir)

dnl Python plugin Directory
AC_DEFINE_UNQUOTED(PYTHON_PLUGIN_DIR, "${myprefix}/${DATADIRNAME}/${PACKAGE}/python", [Gcompris python plugins directory])
PYTHON_PLUGIN_DIR="${myprefix}/${DATADIRNAME}/${PACKAGE}/python"
AC_SUBST(PYTHON_PLUGIN_DIR)

AC_DEFINE_UNQUOTED(GNOME_ICONDIR, "${myprefix}/${DATADIRNAME}/pixmaps", [Gnome icons directory])


dnl Test for gnuchess
AC_PATH_PROG(GNUCHESS, gnuchess,no,[/usr/bin:/usr/games:/usr/local/bin:$PATH])

if test x$GNUCHESS = xno; then
  AC_MSG_ERROR(Couldn't find gnuchess, please install the gnuchess package version 5 or above)
fi
AC_DEFINE_UNQUOTED(GNUCHESS, "$GNUCHESS", Defines where GNU Chess resides on the system)

AC_PATH_PROG(TEXINFO, makeinfo,no)
if test x$TEXINFO = xno; then
  AC_MSG_WARN(Couldn't find texinfo, docs are not compiled. please install the texinfo package)
fi
AM_CONDITIONAL(TEXINFO, test x$TEXINFO != xno)

AC_PATH_PROG(TETEX, texi2html,no)
if test x$TETEX = xno; then
  AC_MSG_WARN(Couldn't find texi2html usualy in the tetex package, please install it)
fi


dnl check for python
AC_ARG_WITH(python,AC_HELP_STRING(
    [--with-python=path],
    [set the path for python]), python_path=$withval, python_path=not)
if test x$python_path = xno ; then
build_python_plugin="no"
else


if test x$python_path != xnot ; then
PYTHON=$python_path
fi
AM_PATH_PYTHON(2.2)
AM_CHECK_PYTHON_HEADERS( [build_python_plugin="yes"], [build_python_plugin="no"] )

fi

dnl Finally output the required python variables and automake defines.
AM_CONDITIONAL(PYTHON_PLUGIN, test x$build_python_plugin = xyes)

dnl build only support
AC_ARG_ENABLE(py-build-only,
          AC_HELP_STRING(
          [--enable-py-build-only],
          [Skip python modules tests. (Useful if you just need build)]),
          py_build_only="$enableval", py_build_only="no")

REQUIRE_PYHTON=""
if test x$build_python_plugin = xno; then
  AC_MSG_NOTICE([Python plugin is disabled.])
else
  dnl Python interpreter is available so check for pygtk and gnome-python
  PKG_CHECK_MODULES(PYGTK, pygtk-2.0 gnome-python-2.0)
  if test x$py_build_only = xno; then
    AM_CHECK_PYMOD(gtk,,,AC_MSG_ERROR([*** pygtk installed but not visible from python ]))
    AM_CHECK_PYMOD(gnome.canvas,,,AC_MSG_ERROR([*** pygtk installed but not visible from python ]))
    AM_CHECK_PYMOD(xml.dom.DOMImplementation,,,AC_MSG_WARN([*** pyxml missing ]))


dnl with gnome-python >= 2.10 gnome.canvas is obsolete. use gnomecanvas 
dnl gnomecanvas not supported yet in gcompris
dnl
dnl   AM_CHECK_PYMOD(gnome.canvas,,test_gnomecanvas="no",test_gnomevanvas="yes")
dnl  if test x$test_gnomecanvas = xyes ; then
dnl     AM_CHECK_PYMOD(gnomecanvas,,,AC_MSG_ERROR([*** gnome.canvas or gnomecanvas installed but not visible from python ]))
dnl   fi

  else
    AC_MSG_WARN([py-build-only: Skipping python modules dependancies])
    AC_MSG_WARN([py-build-only: Do not forget to install pygtk, gnome-python an pyxml])
  fi
fi

AC_SUBST(REQUIRE_PYTHON)

dnl Check for SQLITE
if test x$with_profils = xyes; then
   PKG_CHECK_MODULES(SQLITE3, sqlite3,, AC_MSG_ERROR([*** SQLITE3 not found!]))
   use_profils=yes
   AC_DEFINE([USE_PROFILS],[],[Profiles usage])	
else
   use_profils=no
fi

AM_CONDITIONAL(USE_PROFILS, test x$use_profils = xyes)
AC_SUBST(SQLITE3_CFLAGS)
AC_SUBST(SQLITE3_LIBS)

dnl PYCAIRO SUPPORT 
dnl Check for pycairo
AC_ARG_ENABLE(cairo,
          AC_HELP_STRING(
          [--enable-cairo],
          [Enable cairo support (experimental)]),
          with_cairo="$enableval", with_cairo="no")

use_cairo=no
if test x$with_cairo = xyes; then
   PKG_CHECK_MODULES(CAIRO, cairo >= 0.5.0,, AC_MSG_ERROR([*** cairo not found! get it at http://cairographics.org/ if you want --enable-cairo ]))
   if test x$py_build_only = xno; then
      if test x$build_python_plugin = xyes; then
         PKG_CHECK_MODULES(PYCAIRO, pycairo >= 0.5.0,, AC_MSG_ERROR([*** pycairo not found! get it at http://cairographics.org/ if you want --enable-cairo ]))
         AM_CHECK_PYMOD(cairo,,,AC_MSG_ERROR([*** pycairo installed but not visible from python ]))		 
         use_cairo=yes
         AC_DEFINE([USE_CAIRO],[],[use Cairo graphic library])
      else
         AC_MSG_WARN([ cairo enabled without python ?])
      fi
   else
    AC_MSG_WARN([py-build-only: skipping test. Do not forget to install pycairo !])
    use_cairo=yes
    AC_DEFINE([USE_CAIRO],[],[use Cairo graphic library])
   fi
fi

AM_CONDITIONAL(USE_CAIRO, test x$use_cairo = xyes)
AC_SUBST(CAIRO_CFLAGS)
AC_SUBST(CAIRO_LIBS)


dnl For libassetml
ASSETML_DIR=assetml
AC_SUBST(ASSETML_DIR)
AC_DEFINE_UNQUOTED(ASSETML_DIR, "${myprefix}/${DATADIRNAME}/${ASSETML_DIR}", [Global assetml directory])

dnl assetml version management
# DA
VERSION_VOICES_ALPHABET_DA=1.0
AC_SUBST(VERSION_VOICES_ALPHABET_DA)

VERSION_VOICES_COLORS_DA=1.0
AC_SUBST(VERSION_VOICES_COLORS_DA)

VERSION_VOICES_GEOGRAPHY_DA=1.0
AC_SUBST(VERSION_VOICES_GEOGRAPHY_DA)

VERSION_VOICES_MISC_DA=1.1
AC_SUBST(VERSION_VOICES_MISC_DA)

# DE
VERSION_VOICES_ALPHABET_DE=1.0
AC_SUBST(VERSION_VOICES_ALPHABET_DE)

VERSION_VOICES_COLORS_DE=1.0
AC_SUBST(VERSION_VOICES_COLORS_DE)

VERSION_VOICES_GEOGRAPHY_DE=0.0
AC_SUBST(VERSION_VOICES_GEOGRAPHY_DE)

VERSION_VOICES_MISC_DE=1.0
AC_SUBST(VERSION_VOICES_MISC_DE)

# EN
VERSION_VOICES_ALPHABET_EN=1.1
AC_SUBST(VERSION_VOICES_ALPHABET_EN)

VERSION_VOICES_COLORS_EN=1.1
AC_SUBST(VERSION_VOICES_COLORS_EN)

VERSION_VOICES_GEOGRAPHY_EN=1.2
AC_SUBST(VERSION_VOICES_GEOGRAPHY_EN)

VERSION_VOICES_MISC_EN=1.1
AC_SUBST(VERSION_VOICES_MISC_EN)

# ES
VERSION_VOICES_ALPHABET_ES=1.0
AC_SUBST(VERSION_VOICES_ALPHABET_ES)

VERSION_VOICES_COLORS_ES=1.0
AC_SUBST(VERSION_VOICES_COLORS_ES)

VERSION_VOICES_GEOGRAPHY_ES=1.0
AC_SUBST(VERSION_VOICES_GEOGRAPHY_ES)

VERSION_VOICES_MISC_ES=1.0
AC_SUBST(VERSION_VOICES_MISC_ES)

# FR
VERSION_VOICES_ALPHABET_FR=1.1
AC_SUBST(VERSION_VOICES_ALPHABET_FR)

VERSION_VOICES_COLORS_FR=1.0
AC_SUBST(VERSION_VOICES_COLORS_FR)

VERSION_VOICES_GEOGRAPHY_FR=1.1
AC_SUBST(VERSION_VOICES_GEOGRAPHY_FR)

VERSION_VOICES_MISC_FR=1.1
AC_SUBST(VERSION_VOICES_MISC_FR)

VERSION_VOICES_FRANCE_REGIONS_FR=1.0
AC_SUBST(VERSION_VOICES_FRANCE_REGIONS_FR)

# IT
VERSION_VOICES_ALPHABET_IT=1.1
AC_SUBST(VERSION_VOICES_ALPHABET_IT)

VERSION_VOICES_COLORS_IT=1.1
AC_SUBST(VERSION_VOICES_COLORS_IT)

VERSION_VOICES_GEOGRAPHY_IT=1.1
AC_SUBST(VERSION_VOICES_GEOGRAPHY_IT)

VERSION_VOICES_MISC_IT=1.1
AC_SUBST(VERSION_VOICES_MISC_IT)

# PT
VERSION_VOICES_ALPHABET_PT=1.0
AC_SUBST(VERSION_VOICES_ALPHABET_PT)

VERSION_VOICES_COLORS_PT=1.0
AC_SUBST(VERSION_VOICES_COLORS_PT)

VERSION_VOICES_GEOGRAPHY_PT=1.1
AC_SUBST(VERSION_VOICES_GEOGRAPHY_PT)

VERSION_VOICES_MISC_PT=1.1
AC_SUBST(VERSION_VOICES_MISC_PT)

# RU
VERSION_VOICES_ALPHABET_RU=1.0
AC_SUBST(VERSION_VOICES_ALPHABET_RU)

VERSION_VOICES_COLORS_RU=1.0
AC_SUBST(VERSION_VOICES_COLORS_RU)

VERSION_VOICES_GEOGRAPHY_RU=1.0
AC_SUBST(VERSION_VOICES_GEOGRAPHY_RU)

VERSION_VOICES_MISC_RU=1.0
AC_SUBST(VERSION_VOICES_MISC_RU)

# SV
VERSION_VOICES_ALPHABET_SV=1.0
AC_SUBST(VERSION_VOICES_ALPHABET_SV)

VERSION_VOICES_COLORS_SV=1.0
AC_SUBST(VERSION_VOICES_COLORS_SV)

VERSION_VOICES_GEOGRAPHY_SV=1.0
AC_SUBST(VERSION_VOICES_GEOGRAPHY_SV)

VERSION_VOICES_MISC_SV=1.0
AC_SUBST(VERSION_VOICES_MISC_SV)

#FLAGS
VERSION_FLAGS=1.8
AC_SUBST(VERSION_FLAGS)

AC_SUBST(CFLAGS)

dnl Autoconf output
AC_OUTPUT([ gcompris.spec
Makefile
src/Makefile
src/gcompris/Makefile
src/gcompris/libgcompris-1.0.pc
src/gcompris/libgcompris-1.0-uninstalled.pc
src/boards/Makefile
src/boards/python/Makefile
src/boards/python/gcompris/Makefile
src/boards/python/gcompris/anim/Makefile
src/boards/python/gcompris/bonus/Makefile
src/boards/python/gcompris/score/Makefile
src/boards/python/gcompris/skin/Makefile
src/boards/python/gcompris/sound/Makefile
src/boards/python/gcompris/timer/Makefile
src/boards/python/gcompris/utils/Makefile
src/boards/python/connect4p/Makefile
po/Makefile.in
docs/Makefile
docs/C/Makefile
docs/fr/Makefile
boards/Makefile
boards/advanced_colors/Makefile
boards/babymatch/Makefile
boards/babyshapes/Makefile
boards/chronos/Makefile
boards/doubleentry/Makefile
boards/flags/Makefile
boards/gcompris/Makefile
boards/gcompris/mimetypes/Makefile
boards/geography/Makefile
boards/imageid/Makefile
boards/imagename/Makefile
boards/missing_letter/Makefile
boards/paintings/Makefile
boards/read_colors/Makefile
boards/scales/Makefile
boards/sounds/Makefile
boards/sounds/da/Makefile
boards/sounds/da/alphabet/Makefile
boards/sounds/da/colors/Makefile
boards/sounds/da/geography/Makefile
boards/sounds/da/misc/Makefile
boards/sounds/de/Makefile
boards/sounds/de/alphabet/Makefile
boards/sounds/de/colors/Makefile
boards/sounds/de/geography/Makefile
boards/sounds/de/misc/Makefile
boards/sounds/en/Makefile
boards/sounds/en/alphabet/Makefile
boards/sounds/en/colors/Makefile
boards/sounds/en/geography/Makefile
boards/sounds/en/misc/Makefile
boards/sounds/es/Makefile
boards/sounds/es/alphabet/Makefile
boards/sounds/es/colors/Makefile
boards/sounds/es/geography/Makefile
boards/sounds/es/misc/Makefile
boards/sounds/fr/Makefile
boards/sounds/fr/alphabet/Makefile
boards/sounds/fr/colors/Makefile
boards/sounds/fr/france_region/Makefile
boards/sounds/fr/geography/Makefile
boards/sounds/fr/misc/Makefile
boards/sounds/it/Makefile
boards/sounds/it/alphabet/Makefile
boards/sounds/it/colors/Makefile
boards/sounds/it/geography/Makefile
boards/sounds/it/misc/Makefile
boards/sounds/pt/Makefile
boards/sounds/pt/alphabet/Makefile
boards/sounds/pt/colors/Makefile
boards/sounds/pt/geography/Makefile
boards/sounds/pt/misc/Makefile
boards/sounds/ru/Makefile
boards/sounds/ru/alphabet/Makefile
boards/sounds/ru/colors/Makefile
boards/sounds/ru/geography/Makefile
boards/sounds/ru/misc/Makefile
boards/sounds/sv/Makefile
boards/sounds/sv/alphabet/Makefile
boards/sounds/sv/colors/Makefile
boards/sounds/sv/geography/Makefile
boards/sounds/sv/misc/Makefile
])

echo ""
echo "***************************************************"
echo ""
echo configure complete,
echo with options:
echo "python plugins = $build_python_plugin"
echo "gcompris edit  = $build_gcompris_edit"

if test -x "${SDL_LIBS}"; then
echo "SDL LIBS       = NOT DETECTED (install sdl-devel)"
else
echo "SDL LIBS       = FOUND"
fi

echo ""
if test "x$found_randr" = "xno"; then
echo "XRANDR option  = NO"
else
echo "XRANDR option  = YES"
fi

echo ""
if test "x$use_profils" = "xyes"; then
echo "USE_PROFILS option  = YES"
else
echo "USE_PROFILS option  = NO"
fi

echo gcompris will be installed in ${prefix}
echo to compile and install in in another directory
echo type sh configure --prefix=/usr
echo
echo type \'make\' to compile gcompris
echo type \'make install\' to install it
